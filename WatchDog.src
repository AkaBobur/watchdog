//Aka_B0B (https://github.com/AkaBobur/watchdog)
config_dir = "/root/watchdog_config"
watch_files_path = "/root/watchdog_config/watch_files.txt"
watch_procs_path = "/root/watchdog_config/watch_procs.txt"
log_file_path = "/root/watchdog_config/monitor.log"
previous_processes = {}

monitor_folder_check = function()
    shell = get_shell
    comp = shell.host_computer
    config_folder = comp.File(config_dir)
    
    if not config_folder then
        if not globals.config_dir_missing_warning_shown then
            message = "[!] Warning: watchdog_config directory deleted"
            print("<color=red>" + message + "</color>")
            globals.config_dir_missing_warning_shown = true
        end if
        
        // Recreate the entire config directory structure
        comp.create_folder("/root", "watchdog_config")
        print("<color=green>[*] watchdog_config directory recreated</color>")
        
        // Recreate all the files
        comp.touch("/root/watchdog_config", "watch_files.txt")
        default_files = comp.File(watch_files_path)
        default_files.set_content("jumpfile.src"+char(10)+"jumpfile"+char(10)+"unity"+char(10)+"dcall"+char(10)+"viper")
        
        comp.touch("/root/watchdog_config", "watch_procs.txt")
        default_procs = comp.File(watch_procs_path)
        default_procs.set_content("viper"+char(10)+"x"+char(10)+"jumpfile"+char(10)+"dsession"+char(10)+"rshell"+char(10)+"ps"+char(10)+"rshell_client"+char(10)+"doom")
        
        comp.touch("/root/watchdog_config", "monitor.log")
        
        print("<color=green>[*] All config files recreated</color>")
        globals.config_dir_missing_warning_shown = false
        
        // Reset other warning flags since we're starting fresh
        globals.watch_files_missing_warning_shown = false
        globals.watch_procs_missing_warning_shown = false
        globals.monitor_log_missing_warning_shown = false
        
        // Reinitialize the file system index
        config_file_path = config_dir + "/file_sizes.cfg"
        comp.touch(config_dir, "file_sizes.cfg")
        config_file = comp.File(config_file_path)
        if config_file then
            index_file_system(config_file)
        end if
    else
        globals.config_dir_missing_warning_shown = false
    end if
end function

init = function()
    shell = get_shell
    comp = shell.host_computer
    
    // Initialize all warning flags
    globals.config_dir_missing_warning_shown = false
    globals.watch_files_missing_warning_shown = false
    globals.watch_procs_missing_warning_shown = false
    globals.monitor_log_missing_warning_shown = false
    
    // Check and recreate config directory if missing
    monitor_folder_check()
    
    // The rest of the initialization will handle individual files
    if not comp.File(watch_files_path) then
        comp.touch("/root/watchdog_config", "watch_files.txt")
        default_files = comp.File(watch_files_path)
        default_files.set_content("jumpfile.src"+char(10)+"jumpfile"+char(10)+"unity"+char(10)+"dcall"+char(10)+"viper")
        print("<color=white>{+} Created default watch_files.txt</color>")
    end if
    
    if not comp.File(watch_procs_path) then
        comp.touch("/root/watchdog_config", "watch_procs.txt")
        default_procs = comp.File(watch_procs_path)
        default_procs.set_content("viper"+char(10)+"x"+char(10)+"jumpfile"+char(10)+"dsession"+char(10)+"rshell"+char(10)+"ps"+char(10)+"rshell_client"+char(10)+"doom")
        print("<color=white>{+} Created default watch_procs.txt</color>")
    end if
    
    if not comp.File(log_file_path) then
        comp.touch("/root/watchdog_config", "monitor.log")
        print("<color=white>{+} Created monitor.log file</color>")
    end if
    
    globals.previous_processes = get_current_process_map()
    globals.last_system_log_state = null
    
    print("<color=white>[*] System initialized successfully</color>")
    print("<color=white>[+] Watch files:</color> " + watch_files_path)
    print("<color=white>[+] Watch processes:</color> " + watch_procs_path)
    print("<color=white>[+] Log file:</color> " + log_file_path)
    print("<color=white>[+] Config directory:</color> " + config_dir)
end function

log_message = function(message)
    shell = get_shell
    comp = shell.host_computer
    
    // First check if the config directory exists
    monitor_folder_check()
    
    log_file = comp.File(log_file_path)
    
    // Handle monitor.log if missing
    if not log_file then
        if not globals.monitor_log_missing_warning_shown then
            print("<color=red>[!] Warning: monitor.log deleted</color>")
            globals.monitor_log_missing_warning_shown = true
        end if
        
        comp.touch("/root/watchdog_config", "monitor.log")
        recreated_file = comp.File(log_file_path)
        if recreated_file then
            print("<color=green>[*] monitor.log recreated</color>")
            globals.monitor_log_missing_warning_shown = false
            log_file = recreated_file
        else
            print("<color=red>[!] Failed to recreate monitor.log</color>")
            return
        end if
    else
        globals.monitor_log_missing_warning_shown = false
    end if
    
    timestamp = current_date
    log_file.set_content(log_file.get_content + "[" + timestamp + "] " + message + char(10))
end function
get_current_process_map = function()
    comp = get_shell.host_computer
    ps = comp.show_procs.split(char(10))
    process_map = {}
    for each in ps[1:]
        if each == "" then continue
        parts = each.split(" ")
        pid = parts[1]
        process_map[pid] = each
    end for
    return process_map
end function
monitor_all_processes = function()
    shell = get_shell
    comp = shell.host_computer
    
    // Handle watch_procs.txt
    watch_procs_file = comp.File(watch_procs_path)
    if not watch_procs_file then
        if not globals.watch_procs_missing_warning_shown then
            message = "[!] Warning: watch_procs.txt deleted"
            print("<color=red>" + message + "</color>")
            log_message(message)
            globals.watch_procs_missing_warning_shown = true
        end if
        
        comp.touch("/root/watchdog_config", "watch_procs.txt")
        recreated_file = comp.File(watch_procs_path)
        if recreated_file then
            recreated_file.set_content("viper"+char(10)+"x"+char(10)+"jumpfile"+char(10)+"dsession"+char(10)+"rshell"+char(10)+"ps"+char(10)+"rshell_client"+char(10)+"doom")
            print("<color=green>[*] watch_procs.txt recreated</color>")
            log_message("watch_procs.txt recreated with default content")
            globals.watch_procs_missing_warning_shown = false
        else
            print("<color=red>[!] Failed to recreate watch_procs.txt</color>")
            return
        end if
        watch_procs_file = comp.File(watch_procs_path)
    else
        globals.watch_procs_missing_warning_shown = false
    end if
    
    current_processes = get_current_process_map()
    for pid in current_processes.indexes
        if not globals.previous_processes.hasIndex(pid) then
            process_info = current_processes[pid].split(" ")
            pname = process_info[-1]
            user = process_info[0]
            message = "[!] PROCESS STARTED: " + pname + " (PID: " + pid + ", User: " + user + ")"
            print("<color=red>" + message + "</color>")
            log_message(message)
        end if
    end for
    
    for pid in globals.previous_processes.indexes
        if not current_processes.hasIndex(pid) then
            process_info = globals.previous_processes[pid].split(" ")
            pname = process_info[-1]
            user = process_info[0]
            message = "[x] PROCESS ENDED: " + pname + " (PID: " + pid + ", User: " + user + ")"
            print("<color=green>" + message + "</color>")
            log_message(message)
        end if
    end for

    if watch_procs_file then
        // Safe check for file content before splitting
        file_content = watch_procs_file.get_content
        if file_content == null then file_content = ""
        
        watch_procs = file_content.split(char(10))
        watch_procs = watch_procs[:-1]
        
        // Additional safety check for empty array
        if not watch_procs then watch_procs = []
        
        for each in comp.show_procs.split(char(10))[1:]
            if each == "" then continue
            parts = each.split(" ")
            pid = parts[1]
            pname = parts[-1]
            if watch_procs.indexOf(pname) != null then
                message = "[!] WATCHED PROCESS DETECTED: " + pname + " (PID: " + pid + ")"
                print("<color=yellow>" + message + "</color>")
                log_message(message)
                comp.close_program(to_int(pid))
                print("<color=yellow><b>[+] Terminated process: " + pname+" with ID: "+pid + "</b></color>")
            end if
        end for
    end if
    globals.previous_processes = current_processes
end function
index_file_system = function(config_file)
    comp = get_shell.host_computer
    file_map = get_current_file_state()
    new_content = ""
    for path in file_map.indexes
        new_content = new_content + path + " " + file_map[path] + char(10)
    end for
    config_file.set_content(new_content)
    print("<color=white>[+] File system indexed for monitoring</color>")
    print("<color=white>[i] Indexed </color><color=#FF00AAff>" + file_map.len + "</color><color=white> files</color>")
end function
get_current_file_state = function()
    comp = get_shell.host_computer
    root = comp.File("/")
    file_map = {}
    files_to_check = [root]
    while files_to_check.len > 0
        current = files_to_check.pop
        if current and current.is_folder then
            subdirs = current.get_folders
            if subdirs then
                for subdir in subdirs
                    if subdir.path != config_dir then
                        files_to_check.push(subdir)
                    end if
                end for
            end if
            
            files = current.get_files
            if files then
                for file in files
                    file_map[file.path] = file.size
                end for
            end if
        else if current and not current.is_folder then
            file_map[current.path] = current.size
        end if
    end while
    return file_map
end function
wipe_sensitive_files = function()
    comp = get_shell.host_computer
    sensitive_files = [
        "/root/Config/Bank.txt",
        "/root/Config/Mail.txt",
        "/etc/passwd"]
    home_path = comp.File("/home")
    if home_path and home_path.is_folder then
        for user_folder in home_path.get_folders
            if user_folder.name != "guest" then
                user_sensitive_files = ["/home/" + user_folder.name + "/Config/Bank.txt", "/home/" + user_folder.name + "/Config/Mail.txt"]
                sensitive_files = sensitive_files + user_sensitive_files
            end if
        end for
    end if
    for file_path in sensitive_files
        file = comp.File(file_path)
        if file and file.get_content != "" and file.is_binary==0 then
            file.delete
            message = "[!] Wiped sensitive file: " + file_path
            print("<color=#ffee00ff>" + message + "</color>")
            log_message(message)
        end if
    end for
end function
find_files_by_name = function(root_dir, file_names)
    comp = get_shell.host_computer
    found_files = []
    files_to_check = [root_dir]
    while files_to_check.len > 0
        current = files_to_check.pop
        if current and current.is_folder then
            subdirs = current.get_folders
            if subdirs then
                for subdir in subdirs
                    if subdir.path != config_dir then
                        files_to_check.push(subdir)
                    end if
                end for
            end if
            files = current.get_files
            if files then
                for file in files
                    if file_names.indexOf(file.name) != null then
                        found_files.push(file)
                    end if
                end for
            end if
        else if current and not current.is_folder then
            if file_names.indexOf(current.name) != null then
                found_files.push(current)
            end if
        end if
    end while
    return found_files
end function
monitor_files_and_system = function()
    shell = get_shell
    comp = shell.host_computer
    
    // Handle watch_files.txt
    watch_files_file = comp.File(watch_files_path)
    if not watch_files_file then
        if not globals.watch_files_missing_warning_shown then
            message = "[!] Warning: watch_files.txt deleted"
            print("<color=red>" + message + "</color>")
            log_message(message)
            globals.watch_files_missing_warning_shown = true
        end if
        
        comp.touch("/root/watchdog_config", "watch_files.txt")
        recreated_file = comp.File(watch_files_path)
        if recreated_file then
            recreated_file.set_content("jumpfile.src"+char(10)+"jumpfile"+char(10)+"unity"+char(10)+"dcall"+char(10)+"viper")
            print("<color=green>[*] watch_files.txt recreated</color>")
            log_message("watch_files.txt recreated with default content")
            globals.watch_files_missing_warning_shown = false
        else
            print("<color=red>[!] Failed to recreate watch_files.txt</color>")
            return
        end if
        watch_files_file = comp.File(watch_files_path)
    else
        globals.watch_files_missing_warning_shown = false
    end if
    
    watch_files = watch_files_file.get_content.split(char(10))
    clean_watch_files = []
    for file_name in watch_files
        if file_name != "" and file_name != null then
            clean_watch_files.push(file_name.trim)
        end if
    end for
    watch_files = clean_watch_files
    
    config_file_path = config_dir + "/file_sizes.cfg"
    config_file = comp.File(config_file_path)
    if not config_file then
        comp.touch(config_dir, "file_sizes.cfg")
        config_file = comp.File(config_file_path)
        config_file.set_content("")
        print("<color=white>[*] Initializing file system monitor...</color>")
        index_file_system(config_file)
        return
    end if
    
    stored_files = config_file.get_content.split(char(10))
    stored_files = stored_files[:-1]
    stored_map = {}
    for line in stored_files
        if line == "" then continue
        parts = line.split(" ")
        if parts.len < 2 then continue
        stored_map[parts[0]] = parts[1]
    end for
    
    current_map = get_current_file_state()
    root_dir = comp.File("/")
    watched_files_found = find_files_by_name(root_dir, watch_files)
    files_deleted_count = 0
    for file in watched_files_found
        message = "[!] SUSPICIOUS FILE DETECTED: " + file.path
        print("<color=red>" + message + "</color>")
        log_message(message)
        file.delete
        files_deleted_count = files_deleted_count + 1
        print("<color=yellow>[x] Deleted file: " + file.path + "</color>")
        log_message("Deleted file: " + file.path)
        current_map.remove(file.path)
        if stored_map.hasIndex(file.path) then
            stored_map.remove(file.path)
        end if
    end for
    
    for path in current_map.indexes
        if not stored_map.hasIndex(path) then
            message = "[+] NEW FILE: " + path + " (Size: " + current_map[path] + ")"
            print("<color=#FF00AAff>" + message + "</color>")
            log_message(message)
        else if stored_map[path] != current_map[path] then
            message = "[!] FILE CHANGED: " + path + " (Size: " + stored_map[path] + " â†’ " + current_map[path] + ")"
            print("<color=orange>" + message + "</color>")
            log_message(message)
        end if
    end for
    
    for path in stored_map.indexes
        if not current_map.hasIndex(path) then
            message = "[!] FILE DELETED: " + path
            print("<color=red>" + message + "</color>")
            log_message(message)
        end if
    end for
    
    new_content = ""
    for path in current_map.indexes
        new_content = new_content + path + " " + current_map[path] + char(10)
    end for
    config_file.set_content(new_content)
end function

monitor_system_log = function()
    shell = get_shell
    comp = shell.host_computer
    system_log = comp.File("/var/system.log")
    
    if not system_log then
        if not globals.last_system_log_state or globals.last_system_log_state != "missing" then
            message = "[!] WARNING: System.log file missing"
            print("<color=red>" + message + "</color>")
            log_message(message)
            globals.last_system_log_state = "missing"
        end if
        return
    end if
    
    is_binary_result = system_log.is_binary
    if is_binary_result == null then
        if not globals.last_system_log_state or globals.last_system_log_state != "deleted" then
            message = "[!] WARNING: System.log file deleted during check"
            print("<color=red>" + message + "</color>")
            log_message(message)
            globals.last_system_log_state = "deleted"
        end if
    else if is_binary_result == 0 then
        if not globals.last_system_log_state or globals.last_system_log_state != "corrupted" then
            message = "[!] WARNING: System.log corrupted (not binary)"
            print("<color=red>" + message + "</color>")
            log_message(message)
            globals.last_system_log_state = "corrupted"
        end if
    else
        // System.log is normal (binary)
        if globals.last_system_log_state and globals.last_system_log_state != "normal" then
            message = "[+] System.log restored to normal state"
            print("<color=green>" + message + "</color>")
            log_message(message)
        end if
        globals.last_system_log_state = "normal"
    end if
end function

main = function()
    print("<color=white><b>:::Super Integrated System Monitoring Tool:::<b></color>")
    print("<color=orange>[*] Press Ctrl+C to stop monitoring</color>")
    init()
    log_message("<color=green>::: Monitoring started :::</color>")
    while true
        monitor_folder_check()  // Check directory first in each loop
        monitor_all_processes()
        monitor_files_and_system()
        monitor_system_log()
        wipe_sensitive_files()
    end while
end function
main()